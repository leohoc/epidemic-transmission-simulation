plugins {
	id 'java'
	id "org.sonarqube" version "2.8"
	id 'jacoco'
}

group = 'com.leohoc'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

sonarqube {
	properties {
		property "sonar.projectKey", "leohoc_epidemic-transmission-simulation"
		property "sonar.organization", "leohoc"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

dependencies {
	// Junit + Mockito
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.7
			}
			excludes = [
					'com.leohoc.ets.infrastructure.config.*',
					'com.leohoc.ets.ETSApplication',
                    'com.leohoc.ets.userinterface.GraphicalEnvironment',
					'com.leohoc.ets.userinterface.GraphicalEnvironment.*'
			]
		}
	}
}

check.dependsOn jacocoTestCoverageVerification

task fatJar(type: Jar) {
	zip64 true
	manifest {
		attributes 'Main-Class': 'com.leohoc.ets.ETSApplication'
		exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
	}
	from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	with jar
}